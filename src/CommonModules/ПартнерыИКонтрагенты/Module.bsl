
&Вместо("ПолучитьКонтактноеЛицоПартнераПоУмолчанию")
Функция табАид_ПолучитьКонтактноеЛицоПартнераПоУмолчанию(Партнер)
	
	//+табАид
	КонтактноеЛицоТАБАИД = табАид_ПолучитьКонтактноеЛицоПартнера(Партнер);
	Если ЗначениеЗаполнено(КонтактноеЛицоТАБАИД) Тогда
		Возврат КонтактноеЛицоТАБАИД;
	КонецЕсли;
	//-табАид
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|КонтактныеЛицаПартнеров.Ссылка КАК КонтактноеЛицо
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|ГДЕ
	|	КонтактныеЛицаПартнеров.Владелец = &Партнер
	|	И КонтактныеЛицаПартнеров.ПометкаУдаления = ЛОЖЬ
	|	И (КонтактныеЛицаПартнеров.ДатаПрекращенияСвязи > &ТекущаяДата
	|			ИЛИ КонтактныеЛицаПартнеров.ДатаПрекращенияСвязи = &ПустаяДата)";
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.КонтактноеЛицо;
	
	
КонецФункции

Функция табАид_ПолучитьКонтактноеЛицоПартнера(Партнер) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.Текст = "ВЫБРАТЬ
	|	табАид_КонтактныеЛицаПартнеров.КонтактноеЛицо КАК КонтактноеЛицо
	|ИЗ
	|	РегистрСведений.табАид_КонтактныеЛицаПартнеров КАК табАид_КонтактныеЛицаПартнеров
	|ГДЕ
	|	табАид_КонтактныеЛицаПартнеров.Партнер = &Партнер";
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.КонтактноеЛицо;
	КонецЕсли;
	Возврат Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка();
КонецФункции

