
&НаСервере
&Вместо("ИнициализироватьОтборы")
Процедура табАид_ИнициализироватьОтборы()
	
	Элементы.НазначениеФильтр.СписокВыбора.Очистить();
	Элементы.ШаблонДляФильтр.СписокВыбора.Очистить();
	
	Список.Параметры.УстановитьЗначениеПараметра("Назначение", "");
	
	ВидыШаблонов = ШаблоныСообщенийСлужебный.ВидыШаблонов();
	//+табАид
	
	ВидыШаблонов.Вставить(0, НСтр("ru = 'Электронных писем, SMS и голосовых сообщений'"), НСтр("ru = 'Электронных писем, SMS и голосовых сообщений'"));
	//-табАид
	
	Список.Параметры.УстановитьЗначениеПараметра("СообщениеSMS", ВидыШаблонов.НайтиПоЗначению("SMS").Представление);
	Список.Параметры.УстановитьЗначениеПараметра("ЭлектроннаяПочта", ВидыШаблонов.НайтиПоЗначению("Email").Представление);
	//+табАид
	Список.Параметры.УстановитьЗначениеПараметра("ГолосовоеСообщение", ВидыШаблонов.НайтиПоЗначению("ГолосовоеСообщение").Представление);
	//-табАид
	Список.Параметры.УстановитьЗначениеПараметра("ПоказыватьКонтекстныеШаблоны", Ложь);
	
	Для каждого ВидШаблона Из ВидыШаблонов Цикл
		Элементы.ШаблонДляФильтр.СписокВыбора.Добавить(ВидШаблона.Значение, ВидШаблона.Представление);
	КонецЦикла;
	
	Элементы.НазначениеФильтр.СписокВыбора.Добавить("", НСтр("ru = 'Все'"));
	
	ОбщийПредставление = НСтр("ru = 'Общий'");
	Список.Параметры.УстановитьЗначениеПараметра("Общий",    ОбщийПредставление);
	Элементы.НазначениеФильтр.СписокВыбора.Добавить("Общий", ОбщийПредставление);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ШаблоныСообщений.Назначение КАК Назначение,
	|	ШаблоныСообщений.ПолноеИмяТипаПараметраВводаНаОсновании КАК ПолноеИмяТипаПараметраВводаНаОсновании
	|ИЗ
	|	Справочник.ШаблоныСообщений КАК ШаблоныСообщений
	|ГДЕ
	|	ШаблоныСообщений.Назначение <> """" И ШаблоныСообщений.Назначение <> ""Служебный""
	|	И ШаблоныСообщений.Назначение <> &Общий
	|
	|СГРУППИРОВАТЬ ПО
	|	ШаблоныСообщений.Назначение, ШаблоныСообщений.ПолноеИмяТипаПараметраВводаНаОсновании
	|
	|УПОРЯДОЧИТЬ ПО
	|	Назначение";
	
	Запрос.УстановитьПараметр("Общий", ОбщийПредставление);
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
		Элементы.НазначениеФильтр.СписокВыбора.Добавить(РезультатЗапроса.ПолноеИмяТипаПараметраВводаНаОсновании, РезультатЗапроса.Назначение);
	КонецЦикла;
	
	Назначение = "";
	//+таб
	ШаблонДля = НСтр("ru = 'Электронных писем, SMS и голосовых сообщений'");
	//-таб
КонецПроцедуры

&НаКлиенте
Процедура табАид_ШаблонДляФильтрОбработкаВыбораВместо(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "SMS" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ШаблонДля", НСтр("ru = 'Сообщения SMS'"), ВидСравненияКомпоновкиДанных.Равно);
	ИначеЕсли ВыбранноеЗначение = "Email" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ШаблонДля", НСтр("ru = 'Электронного письма'"), ВидСравненияКомпоновкиДанных.Равно);
		//+табАид
	ИначеЕсли ВыбранноеЗначение = "ГолосовоеСообщение" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ШаблонДля", НСтр("ru = 'Голосовое сообщение'"), ВидСравненияКомпоновкиДанных.Равно);
		//-табАид
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "ШаблонДля");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура табАид_ДобавитьКомандыНаСервере()
	
	//Добавляем новую команду
	КомандаСоздатьГС = ЭтотОбъект.Команды.Добавить("СоздатьШаблонГолосовогоСообщения");
	КомандаСоздатьГС.Действие  = "табАид_СоздатьШаблонГолосовогоСообщения";
	КомандаСоздатьГС.Заголовок = "Голосового сообщения";
	КомандаСоздатьГС.Подсказка = "Создать шаблон голосового сообщения";
	
	//Добавляем новую кнопку
	КнопкаФормыСоздатьГС = Элементы.Добавить("кн_КомандаСоздатьГС", Тип("КнопкаФормы"), Элементы.ФормаГруппаСоздать);
	КнопкаФормыСоздатьГС.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаФормыСоздатьГС.ИмяКоманды = "СоздатьШаблонГолосовогоСообщения";
	
КонецПроцедуры

&НаКлиенте
Процедура табАид_СоздатьШаблонГолосовогоСообщения()
	СоздатьШаблон("ГолосовоеСообщение");	
КонецПроцедуры

&НаКлиенте
&Вместо("СоздатьШаблон")
Процедура табАид_СоздатьШаблон(ТипСообщения)
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ВидСообщения",           ТипСообщения);
	ПараметрыФормы.Вставить("ПолноеИмяТипаОснования", Назначение);
	ПараметрыФормы.Вставить("МожноМенятьНазначение",  Истина);
	//+табАид
	ПараметрыФормы.Вставить("ЭтоТАБАИД",  				  Истина);
	//-табАид
	ОткрытьФорму("Справочник.ШаблоныСообщений.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура табАид_ЗаполнитьШаблоныПоУмолчаниюНаСервере()
	Макет = Справочники.ШаблоныСообщений.ПолучитьМакет("табАид_Макет");
	
	КоличествоСтрок = Макет.ВысотаТаблицы;
	СоответствиеКолонок = табАид_ПолучитьСоответствиеКолонок();
	Масс = новый Массив;
	Для НомерСтроки = 2 По КоличествоСтрок Цикл 
		СтрокаДанных = "";
		СтруктураДанных = Новый Структура();
		Для Каждого ПолеСоответствия Из СоответствиеКолонок Цикл
			ТекущаяОбласть = Макет.ПолучитьОбласть("R"+Формат(НомерСтроки,"ЧГ=0") + "C"+Формат(ПолеСоответствия.Значение,"ЧГ=0")).ТекущаяОбласть;
			ЗначениеЯчейки = СокрЛП(ТекущаяОбласть.Текст);	
			Данные = СокрЛП(ЗначениеЯчейки);
			
			СтруктураДанных.Вставить(ПолеСоответствия.Ключ, Данные);
			СтрокаДанных = СтрокаДанных + Данные;
		КонецЦикла;
		
		Если Не ПустаяСтрока(СтрокаДанных) Тогда	
			Масс.Добавить(СтруктураДанных);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Стр Из Масс Цикл
		ШаблонСсылка = табАид_ПолучитьШаблон(Стр.Имя);
		Если ШаблонСсылка = Неопределено Тогда
			Шаблон = Справочники.ШаблоныСообщений.СоздатьЭлемент();
		иначе
			Шаблон = ШаблонСсылка.ПолучитьОбъект();
		КонецЕсли;	
		
		Если Стр.Вид = "Email"  Тогда
			Шаблон.ТекстШаблонаПисьма = СтрЗаменить(Стр.Текст,"$",""+Символы.ПС+"");
			Шаблон.ТекстШаблонаПисьмаHTML = Стр.Текст;
			Шаблон.Наименование = Стр.Имя;
			Шаблон.Назначение = "ТАБ АИД";
			Шаблон.ПолноеИмяТипаПараметраВводаНаОсновании = "Обработка.табАид_ВиртуальныйМенеджер";
			Шаблон.ТипТекстаПисьма = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст;
			Шаблон.ПредназначенДляВводаНаОсновании = Истина;
			Шаблон.ПредназначенДляЭлектронныхПисем = Истина;
			Шаблон.Записать();
		ИначеЕсли Стр.Вид = "SMS"  Тогда   
			Шаблон.ТекстШаблонаSMS = СтрЗаменить(Стр.Текст,"$",""+Символы.ПС+"");
			Шаблон.ТекстШаблонаПисьмаHTML = Стр.Текст;
			Шаблон.Наименование = Стр.Имя;
			Шаблон.Назначение = "ТАБ АИД";
			Шаблон.ПолноеИмяТипаПараметраВводаНаОсновании = "Обработка.табАид_ВиртуальныйМенеджер";
			Шаблон.ТипТекстаПисьма = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст;
			Шаблон.ПредназначенДляВводаНаОсновании = Истина;
			Шаблон.ПредназначенДляЭлектронныхПисем = Ложь;
			Шаблон.ПредназначенДляSMS = Истина;
			Шаблон.Записать();
		ИначеЕсли Стр.Вид = "ГолосовоеСообщение"  Тогда
			Шаблон.ТекстШаблонаПисьма = СтрЗаменить(Стр.Текст,"$",""+Символы.ПС+"");
			Шаблон.ТекстШаблонаПисьмаHTML = Стр.Текст;
			Шаблон.ТекстШаблонаSMS = СтрЗаменить(Стр.Текст,"$",""+Символы.ПС+"");
			Шаблон.Наименование = Стр.Имя;
			Шаблон.Назначение = "ТАБ АИД";
			Шаблон.ПолноеИмяТипаПараметраВводаНаОсновании = "Обработка.табАид_ВиртуальныйМенеджер";
			Шаблон.ТипТекстаПисьма = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст;
			Шаблон.ПредназначенДляВводаНаОсновании = Истина;
			Шаблон.ПредназначенДляЭлектронныхПисем = Ложь;
			Шаблон.табАид_ПредназначенДляГолосовогоСообщения = Истина;
			Шаблон.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

Функция табАид_ПолучитьШаблон(Имя)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ШаблоныСообщений.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ШаблоныСообщений КАК ШаблоныСообщений
	|ГДЕ
	|	ШаблоныСообщений.ПометкаУдаления = ЛОЖЬ
	|	И ШаблоныСообщений.Наименование = &Наименование";
	Запрос.УстановитьПараметр("Наименование",Имя );
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда
		Возврат РезультатЗапроса.ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции


&НаКлиенте
Процедура табАид_ЗаполнитьШаблоныПоУмолчанию(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения("табАид_ПослеЗакрытияВопроса", ЭтотОбъект, Параметры);
	ПоказатьВопрос(Оповещение,"Осознаю и соглашаюсь, что отправка данных текстов должна производиться в строгом соответствии с законодательством РФ. 
	|  Соглашаясь с вышеуказанным условием, несу полную ответственность за распространение шаблонов  текстов.
	|  Компания ""Технологии и Бизнес"" не ответственность за содержание шаблонов текстов и/или ее представители ни при каких 
	|  обстоятельствах не несут ответственности за ущерб любого характера, полученный в результате использования данных текстов.", Режим, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура табАид_ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	табАид_ЗаполнитьШаблоныПоУмолчаниюНаСервере();
	Элементы.Список.Обновить();
КонецПроцедуры

Функция табАид_ПолучитьСоответствиеКолонок()
	
	СоответствиеКолонок = Новый Соответствие;
	
	СоответствиеКолонок.Вставить("Имя", 1);
	СоответствиеКолонок.Вставить("Вид", 2);
	СоответствиеКолонок.Вставить("Текст", 3);
	
	Возврат СоответствиеКолонок;
	
КонецФункции

&НаСервере
Процедура табАид_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	ЭтотОбъект.Список.ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СправочникШаблоныСообщений.Ссылка КАК Ссылка,
	|	СправочникШаблоныСообщений.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА СправочникШаблоныСообщений.ПредназначенДляSMS
	|			ТОГДА &СообщениеSMS
	|		КОГДА СправочникШаблоныСообщений.табАид_ПредназначенДляГолосовогоСообщения
	|			ТОГДА &ГолосовоеСообщение
	|		ИНАЧЕ &ЭлектроннаяПочта
	|	КОНЕЦ КАК ШаблонДля,
	|	ВЫБОР
	|		КОГДА СправочникШаблоныСообщений.Назначение = """"
	|			ТОГДА &Общий
	|		ИНАЧЕ СправочникШаблоныСообщений.Назначение
	|	КОНЕЦ КАК Назначение,
	|	СправочникШаблоныСообщений.ВладелецШаблона КАК ВладелецШаблона,
	|	0 КАК ЕстьФайлы
	|ИЗ
	|	Справочник.ШаблоныСообщений КАК СправочникШаблоныСообщений
	|ГДЕ
	|	(&Назначение = """"
	|			ИЛИ СправочникШаблоныСообщений.Назначение = &Назначение)
	|	И (СправочникШаблоныСообщений.ВладелецШаблона = НЕОПРЕДЕЛЕНО
	|			ИЛИ СправочникШаблоныСообщений.ВладелецШаблона = ЗНАЧЕНИЕ(Справочник.ИдентификаторыОбъектовМетаданных.ПустаяСсылка)
	|			ИЛИ &ПоказыватьКонтекстныеШаблоны)";
	
	табАид_ДобавитьКомандыНаСервере();
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
	Список,"Назначение","ТАБ АИД",,,Истина);
	
КонецПроцедуры


