
&НаСервере
&После("ИнициализироватьНовыйШаблонСообщений")
Процедура табАид_ИнициализироватьНовыйШаблонСообщений(Знач НастройкиШаблоновСообщений)
	ВидСообщения = Параметры.ВидСообщения;
	Если Параметры.Основание = Неопределено Тогда
		Если ВидСообщения = "ГолосовоеСообщение" Тогда
			Объект.ПредназначенДляSMS = ложь;
			Объект.ПредназначенДляЭлектронныхПисем = Ложь;
			Объект.табАид_ПредназначенДляГолосовогоСообщения = Истина;
			Объект.ТипТекстаПисьма = Перечисления.СпособыРедактированияЭлектронныхПисем.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	Если Параметры.ЭтоТАБАИД Тогда
		Объект.ПолноеИмяТипаПараметраВводаНаОсновании = "Обработка.табАид_ВиртуальныйМенеджер";
		Объект.ПредназначенДляВводаНаОсновании = Истина;
		Объект.Назначение = "ТАБ АИД";
		СформироватьСписокРеквизитовИПечатныхФорм();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
&Перед("УстановитьВыборФормата")
Процедура табАид_УстановитьВыборФормата(Знач ФорматыСохранения=Неопределено)
	Если Объект.табАид_ПредназначенДляГолосовогоСообщения Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаСервере
&Вместо("ОтображениеЭлементовФормы")
Процедура табАид_ОтображениеЭлементовФормы(ФорматаПисьма = "")
	
	Если Объект.ПредназначенДляSMS Тогда
		СуффиксЗаголовка = НСтр("ru = 'Шаблон сообщения SMS'");
		Элементы.ФормаВидТекстаЭлектронногоПисьма.Видимость = Ложь;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СообщениеSMS;
		Элементы.ТемаПисьма.Видимость = Ложь;
		Элементы.СкрытыйЗаголовокПараметры.Видимость = Ложь;
		Элементы.ГруппаВложения.Видимость = Ложь;
		Элементы.РеквизитыКонтекстноеМенюДобавитьПараметрПочта.Видимость = Ложь;
		Элементы.РеквизитыМенюДобавитьПараметрПочта.Видимость = Ложь;
		Элементы.РеквизитыКонтекстноеМенюДобавитьПараметрВТекстСообщенияДляSMS.Видимость = Истина;
		Элементы.РеквизитыМенюДобавитьПараметрВТекстСообщенияSMS.Видимость = Истина;
		Элементы.СкрытыйЗаголовокСообщениеSMS.Видимость = Истина;
		//+табАид
	иначеЕсли Объект.табАид_ПредназначенДляГолосовогоСообщения Тогда
		СуффиксЗаголовка = НСтр("ru = 'Шаблон голосового сообщения'");
		Элементы.ФормаВидТекстаЭлектронногоПисьма.Видимость = Ложь;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СообщениеSMS;
		Элементы.ТемаПисьма.Видимость = Ложь;
		Элементы.СкрытыйЗаголовокПараметры.Видимость = Ложь;
		Элементы.ГруппаВложения.Видимость = Ложь;
		Элементы.РеквизитыКонтекстноеМенюДобавитьПараметрПочта.Видимость = Ложь;
		Элементы.РеквизитыМенюДобавитьПараметрПочта.Видимость = Ложь;
		Элементы.РеквизитыКонтекстноеМенюДобавитьПараметрВТекстСообщенияДляSMS.Видимость = Истина;
		Элементы.РеквизитыМенюДобавитьПараметрВТекстСообщенияSMS.Видимость = Истина;
		Элементы.СкрытыйЗаголовокСообщениеSMS.Видимость = Истина;
		Элементы.ДекорацияСообщениеSMS.Видимость = Ложь;
		Элементы.ОтправлятьВТранслите.Видимость  = Ложь;
		//-табАид
	Иначе
		СуффиксЗаголовка = НСтр("ru = 'Шаблон сообщения электронного письма'");
		Элементы.ГруппаВложения.Видимость = Истина;
		Элементы.РеквизитыКонтекстноеМенюДобавитьПараметрПочта.Видимость = Истина;
		Элементы.РеквизитыМенюДобавитьПараметрПочта.Видимость = Истина;
		Элементы.РеквизитыКонтекстноеМенюДобавитьПараметрВТекстСообщенияДляSMS.Видимость = Ложь;
		Элементы.РеквизитыМенюДобавитьПараметрВТекстСообщенияSMS.Видимость = Ложь;
		
		Если НЕ ФорматаПисьмаПредопределен(ФорматаПисьма) Тогда
			Если Объект.ТипТекстаПисьма = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
				УстановитьЭлектронноеПисьмоHTML();
			Иначе
				УстановитьЭлектронноеПисьмоОбычныйТекст();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Заголовок = Объект.Наименование + " (" + СуффиксЗаголовка + ")";
	Иначе
		Заголовок = СуффиксЗаголовка + " (" + НСтр("ru = 'создание'")+ ")";
	КонецЕсли;
	
	Если Объект.ШаблонПоВнешнейОбработке Тогда
		Элементы.ГруппаНазначение.Доступность = Ложь;
		Элементы.ТемаПисьма.ТолькоПросмотр = Истина;
		Элементы.ГруппаВнешняяОбработка.Видимость = Истина;
		Элементы.ГруппаПараметры.Видимость = Ложь;
		Элементы.ФормаВнешнейОбработкой.Пометка = Истина;
		Элементы.ФормаПоШаблону.Пометка = Ложь;
		ЗаполнитьШаблонПоВнешнейОбработке();
	Иначе
		Элементы.ПолноеИмяТипаПараметраВводаНаОсновании.Доступность = Истина;
		Элементы.ТемаПисьма.ТолькоПросмотр = Ложь;
		Элементы.ГруппаВнешняяОбработка.Видимость = Ложь;
		Элементы.ГруппаПараметры.Видимость = Истина;
		Элементы.ФормаВнешнейОбработкой.Пометка = Ложь;
		Элементы.ФормаПоШаблону.Пометка = Истина;
	КонецЕсли;
	
	Элементы.ГруппаДоступ.Видимость = 
	НЕ ПараметрыДоступа("Изменение", Метаданные.Справочники.ШаблоныСообщений, "Ссылка").ОграничениеУсловием;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
		Элементы.ФормаГруппаСообщениеФормируется.Видимость = МодульДополнительныеОтчетыИОбработки.ИспользуютсяДополнительныеОтчетыИОбработки();
	Иначе
		Элементы.ФормаГруппаСообщениеФормируется.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
		Элементы.ГруппаНастройкиВложений.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
&После("ПередЗаписьюНаСервере")
Процедура табАид_ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ИнформацияОПроверке = ОбработатьТекстШаблона();
	Если ТекущийОбъект.табАид_ПредназначенДляГолосовогоСообщения Тогда
		ТекущийОбъект.ТекстШаблонаSMS = ИнформацияОПроверке.ОбычныйТекст;
		ТекущийОбъект.ФорматВложений = Неопределено;
		ТекущийОбъект.ТекстШаблонаПисьмаHTML = "";
		ТекущийОбъект.ТекстШаблонаПисьма     = "";
	КонецЕсли;
КонецПроцедуры

&НаСервере
&После("УстановитьТекстШаблона")
Процедура табАид_УстановитьТекстШаблона(ТекущийОбъект, СписокФайлов=Неопределено)
	
	Если ТекущийОбъект.табАид_ПредназначенДляГолосовогоСообщения Тогда
		ТелоСообщенияОбычныйТекст.УстановитьТекст(ТекущийОбъект.ТекстШаблонаSMS);
	КонецЕсли;
	ТекущийОбъект.текстШаблонаПисьма = СтрЗаменить(ТекущийОбъект.текстШаблонаПисьма,"~КИ.","");
	ТекущийОбъект.текстШаблонаПисьма = СтрЗаменить(ТекущийОбъект.текстШаблонаПисьма,"Свойство.","");	
КонецПроцедуры

&НаКлиенте
&Вместо("ДобавитьВТекстСообщенияПараметр")
Процедура табАид_ДобавитьВТекстСообщенияПараметр()
	
	Если Элементы.Реквизиты.ВыделенныеСтроки <> Неопределено Тогда
		Текст = "";
		Для каждого НомерСтроки Из Элементы.Реквизиты.ВыделенныеСтроки Цикл
			НайденнаяСтрока = Реквизиты.НайтиПоИдентификатору(НомерСтроки);
			Если НайденнаяСтрока <> Неопределено Тогда
				ФорматВывода = ?(ПустаяСтрока(НайденнаяСтрока.Формат), "", "{" + НайденнаяСтрока.Формат +"}");
				Текст = Текст + "[" + НайденнаяСтрока.ПолноеПредставление + ФорматВывода + "] ";
			КонецЕсли;
		КонецЦикла;
		Если Объект.ТипТекстаПисьма = ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") Тогда
			Если ПустаяСтрока(Элементы.ТелоПисьмаВHTML.ВыделенныйТекст) Тогда
				ЗакладкаДляВставкиНачало = Неопределено;
				ЗакладкаДляВставкиКонец  = Неопределено;
				Элементы.ТелоПисьмаВHTML.ПолучитьГраницыВыделения(ЗакладкаДляВставкиНачало, ЗакладкаДляВставкиКонец);
				ТелоПисьмаВHTML.Вставить(ЗакладкаДляВставкиКонец, Текст);
			Иначе
				Элементы.ТелоПисьмаВHTML.ВыделенныйТекст = Текст;
			КонецЕсли;
		Иначе
			Если Объект.ПредназначенДляSMS Тогда
				Элементы.ТелоСообщенияОбычныйТекстSMS.ВыделенныйТекст = Текст;
			ИначеЕсли Объект.табАид_ПредназначенДляГолосовогоСообщения Тогда 
				Элементы.ТелоСообщенияОбычныйТекстSMS.ВыделенныйТекст = Текст;
			Иначе
				Элементы.ТелоСообщенияОбычныйТекст.ВыделенныйТекст = Текст;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

